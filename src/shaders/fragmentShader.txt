#version 330 core

const int MAX_LIGHTS = 5;

in vec2 pass_textureCoordinates;
in vec3 surfaceNormal;
in vec3 toLightVector[MAX_LIGHTS];
in vec3 toCameraVector;
in float visibility;

out vec4 out_Colour;

uniform sampler2D modelTexture;
uniform vec3 lightColour[MAX_LIGHTS];
uniform float shineDamper;
uniform float reflectivity;
uniform vec3 skyColour;
uniform vec3 attenuation[MAX_LIGHTS];

void main()
{
	vec3 unitNormal = normalize(surfaceNormal);
	vec3 unitVectorToCamera = normalize(toCameraVector);
	
	vec3 totalDiffuse = vec3(0.0f, 0.0f, 0.0f);
	vec3 totalSpecular = vec3(0.0f, 0.0f, 0.0f);
	for (int i = 0; i < MAX_LIGHTS; i++) {
		float distance = length(toLightVector[i]);
		float attenuationFactor = attenuation[i].x + (attenuation[i].y * distance) + (attenuation[i].z * distance * distance);
	
		vec3 unitLightVector = normalize(toLightVector[i]);
		
		float nDotl = dot(unitNormal, unitLightVector);
		float brightness = max(nDotl, 0.0f);
		vec3 lightDirection = -unitLightVector;
		
		vec3 reflectedLightDirection = reflect(lightDirection, unitNormal);
		
		float specularFactor = dot(reflectedLightDirection, unitVectorToCamera);
		specularFactor = max(specularFactor, 0.0f);
		float dampedFactor = pow(specularFactor, shineDamper);
		
		totalDiffuse += brightness * lightColour[i] / attenuationFactor;
		totalSpecular += dampedFactor * reflectivity * lightColour[i] / attenuationFactor;
	}
	
	totalDiffuse = max(totalDiffuse, 0.2f);
	
	vec4 textureColour = texture(modelTexture, pass_textureCoordinates);
	
	if (textureColour.a < 0.5f) {
		discard;
	}
	
	out_Colour = vec4(totalDiffuse, 1.0f) * textureColour + vec4(totalSpecular, 1.0f);
	out_Colour = mix(vec4(skyColour, 1.0f), out_Colour, visibility);
}